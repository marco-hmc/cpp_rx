# avoid repeated including
include_guard(GLOBAL)

function(assign_target_folder_by_type target)
    if(NOT TARGET ${target})
        return()
    endif()
    get_target_property(_type ${target} TYPE)
    if(_type STREQUAL "STATIC_LIBRARY")
        set(_folder "1_lib")
    elseif(_type STREQUAL "SHARED_LIBRARY" OR _type STREQUAL "MODULE_LIBRARY")
        set(_folder "2_dll")
    elseif(_type STREQUAL "EXECUTABLE")
        if("${target}" MATCHES "([Tt]est(s)?$|^test_.*|.*_test$)")
            set(_folder "test")
        else()
            set(_folder "0_exe")
        endif()
    else()
        set(_folder "3_others")
    endif()
    set_target_properties(${target} PROPERTIES FOLDER "${_folder}")
endfunction()

# target_group(target1 target2 ...)
function(target_group)
    foreach(t ${ARGV})
        assign_target_folder_by_type(${t})
    endforeach()
endfunction()

function(target_group_all)
    if(ARGC GREATER 0)
        set(_dirs ${ARGV})
    else()
        set(_dirs ${CMAKE_CURRENT_SOURCE_DIR})
    endif()

    foreach(_d ${_dirs})
        # 当前目录的目标
        get_property(_tgts DIRECTORY ${_d} PROPERTY BUILDSYSTEM_TARGETS)
        foreach(_t ${_tgts})
            assign_target_folder_by_type(${_t})
        endforeach()
        # 递归子目录
        get_property(_subs DIRECTORY ${_d} PROPERTY SUBDIRECTORIES)
        if(_subs)
            target_group_all(${_subs})
        endif()
    endforeach()
endfunction()
